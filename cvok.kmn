c cvok generated from template at 2024-03-16 09:43:41
c with name "cvok"
store(&VERSION) '10.0'
store(&NAME) 'cvok'
store(&COPYRIGHT) '© Tony Mattina'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any windows iphone mobile desktop'
store(&BITMAP) 'cvok.ico'
store(&VISUALKEYBOARD) 'cvok.kvks'
store(&LAYOUTFILE) 'cvok.keyman-touch-layout'

begin Unicode > use(main)
c Colville-Okanagan Keyboard for Unicode fonts
store(&MnemonicLayout) "1"        c This keyboard uses a mnemonic layout.
store(vowel) "aeiou"                                                                     c vowels that can be modified
store(vstress1)  U+00E1  U+00E9  U+00ED  U+00F3  U+00FA    c all vowel symbols with acute
store(vstress2)    U+00E0  U+00E8  U+00EC  U+00F2  U+00F9   c all vowel symbols with grave
store(doubleuse)  "4678QvW|'_:"      c keys that are replaced by phonetic characters
group(Main) using keys
+ '4' >  U+019B U+0313
+ '6' > U+0295
+ '7' > U+0294
+ '8' > U+0263
+ "Q" > U+221A
+ 'v' > U+0259
+ 'W' > U+02B7
+ '|' > U+026C
+ "_" > U+203F
+ ":" > U+02D1
+ "=" > U+F1EA
c glott and laryng segments with comma above
"m" + "'" > "m" U+0313
"n" + "'" > "n" U+0313
"y" + "'" > "y" U+0313
"w" + "'" > "w" U+0313
"p" + "'" > "p" U+0313
"q" + "'" > "q" U+0313
"l" + "'" > "l" U+0313
"k" + "'" > "k" U+0313
"c" + "'" > "c" U+0313
"t" + "'" > "t" U+0313
"r" + "'" > "r" U+0313
U+0263 + "'" > U+0263 U+0313 c gamma
U+0295 + "'" > U+0295  U+0313 c pharyngeal
c stressed schwa
U+0259 + "/" > U+0259 U+0301
U+0259 + "\" > U+0259   U+0300
"H" + "x" > "x" U+030C
"H" + ":" > U+2022 c bullet
any(vowel) + "\" > index(vstress2,1)
any(vowel) + "/" > index(vstress1,1)
+ "0" > deadkey(zero)
"." + "h" > "h" U+0323 c subdot h
deadkey(zero) + any(doubleuse) > index(doubleuse, 2)
deadkey(zero) + "=" > "="
deadkey(zero) + "0" > "0"
+ [T_0070_0313] > U+0070 U+0313 c p̓
+ [T_0074_0313] > U+0074 U+0313 c t̓
+ [T_0063_0313] > U+0063 U+0313 c c̓
+ [T_006B_0313] > U+006B U+0313 c k̓
+ [T_0071_0313] > U+0071 U+0313 c q̓
+ [T_006D_0313] > U+006D U+0313 c m̓
+ [T_006E_0313] > U+006E U+0313 c n̓
+ [T_0072_0313] > U+0072 U+0313 c r̓
+ [T_006C_0313] > U+006C U+0313 c l̓
+ [T_0079_0313] > U+0079 U+0313 c y̓
+ [T_0077_0313] > U+0077 U+0313 c w̓
+ [T_0295_0313] > U+0295 U+0313 c ʕ̓
+ [T_00E1] > U+00E1 c á
+ [T_00ED] > U+00ED c í
+ [T_00FA] > U+00FA c ú
+ [T_0078_030C] > "x" U+030C c x̌
